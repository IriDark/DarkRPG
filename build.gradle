import com.google.gson.Gson
import com.google.gson.GsonBuilder

import java.nio.charset.StandardCharsets

buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        maven { url = 'https://maven.minecraftforge.net' }
        maven { url = 'https://repo.spongepowered.org/repository/maven-public/' }
        maven { url = 'https://maven.parchmentmc.org' }
        maven { url = 'https://jitpack.io' }
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1.+', changing: true
        classpath 'org.spongepowered:mixingradle:0.7-SNAPSHOT'
        classpath 'org.parchmentmc:librarian:1.+'
    }
}

plugins {
    id "com.modrinth.minotaur" version "2.+"
}
apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'eclipse'
apply plugin: 'maven-publish'
apply plugin: 'org.spongepowered.mixin'
apply plugin: 'org.parchmentmc.librarian.forgegradle'

version = '1.20.1-build-0.6b'
group = 'com.github.IriDark'
archivesBaseName = 'Valoria'

java.toolchain.languageVersion = JavaLanguageVersion.of(17)
println('Java: ' + System.getProperty('java.version') + ' JVM: ' + System.getProperty('java.vm.version') + '(' + System.getProperty('java.vendor') + ') Arch: ' + System.getProperty('os.arch'))
minecraft {
    mappings channel: 'parchment', version: '2023.09.03-1.20.1'
    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')
    runs {
        client {
            args "--username", "IriDark_"

            workingDirectory project.file('run')
	        jvmArg "-Dmixin.env.disableRefMap=true"
		
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'

            mods {
                valoria {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run')
	        jvmArg "-Dmixin.env.disableRefMap=true"
		
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'

            mods {
                valoria {
                    source sourceSets.main
                }
            }
        }

        data {
            workingDirectory project.file('run')
            jvmArg "-Dmixin.env.disableRefMap=true"

            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'
            args '--mod', 'valoria', '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/')

            mods {
                valoria {
                    source sourceSets.main
                }
            }
        }
    }
}

mixin {
    add sourceSets.main, "valoria.refmap.json"
    config "valoria.mixins.json"
}

sourceSets.main.resources { srcDir 'src/generated/resources' }
repositories {
    mavenLocal()
    mavenCentral()
    maven {
        name = "Modrinth"
        url = "https://api.modrinth.com/maven"
    }
    maven { url "https://maven.theillusivec4.top/"}
    maven { url "https://cursemaven.com"}
    maven { url "https://maven.blamejared.com"}
    maven { url 'https://jitpack.io' }
}

dependencies {
    minecraft 'net.minecraftforge:forge:1.20.1-47.2.16'
    annotationProcessor 'org.spongepowered:mixin:0.8.5:processor'

    // API's
    compileOnly fg.deobf("top.theillusivec4.curios:curios-forge:5.7.2+1.20.1:api")
    compileOnly fg.deobf("mezz.jei:jei-1.20.1-common-api:15.2.0.27")
    compileOnly fg.deobf("com.blamejared.crafttweaker:CraftTweaker-forge-1.20.1:14.0.13")
    implementation fg.deobf("curse.maven:farmers-delight-398521:4679319") // todo: compat
    implementation fg.deobf("curse.maven:jade-324717:4986594")
    implementation fg.deobf("maven.modrinth:quark:1.20.1-4.0-439")
    implementation fg.deobf("maven.modrinth:zeta:1.20.1-1.0-14")

    // RunTime
    runtimeOnly fg.deobf("top.theillusivec4.curios:curios-forge:5.7.2+1.20.1")
    runtimeOnly fg.deobf("mezz.jei:jei-1.20.1-forge:15.2.0.27")
    runtimeOnly fg.deobf("com.blamejared.crafttweaker:CraftTweaker-forge-1.20.1:14.0.13")
    runtimeOnly fg.deobf("curse.maven:plusutils-979003:5133553")
    runtimeOnly fg.deobf("curse.maven:appleskin-248787:4770828")
  // runtimeOnly fg.deobf("curse.maven:prism-lib-638111:4627141")
  //  runtimeOnly fg.deobf("maven.modrinth:iceberg:1.1.13")
  //  runtimeOnly fg.deobf("maven.modrinth:legendary-tooltips:1.4.5")
    //runtimeOnly fg.deobf("curse.maven:advanced-xray-256256:4840340") // used to test ore balance
    //runtimeOnly fg.deobf("curse.maven:model-gap-fix-676136:4607206")
    //runtimeOnly fg.deobf("curse.maven:just-enough-effect-descriptions-jeed-532286:4978810")
    //runtimeOnly fg.deobf("curse.maven:mutil-351914:4824501")
    //runtimeOnly fg.deobf("curse.maven:tetra-289712:4857842")
}

jar {
    manifest {
        attributes([
                "Specification-Title": "valoria",
                "Specification-Vendor": "IriDark",
                "Specification-Version": "1", // We are version 1 of ourselves
                "Implementation-Title": project.name,
                "Implementation-Version": project.jar.archiveVersion,
                "Implementation-Vendor" :"IriDark",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

static InputStreamReader createInput(File f) throws FileNotFoundException {
    return new InputStreamReader(new FileInputStream(f), StandardCharsets.UTF_8)
}

static OutputStreamWriter createOutput(File f) throws FileNotFoundException {
    return new OutputStreamWriter(new FileOutputStream(f), StandardCharsets.UTF_8)
}

// Thanks to Skoow for helping with this
task updateResources {
    doLast {
        File resourcesFolder = new File("src/main/resources"); resourcesFolder.mkdir()
        File langFolder = new File(resourcesFolder, "assets/valoria/lang"); langFolder.mkdir()
        File mainLangJson = new File(langFolder, "en_us.json"); mainLangJson.createNewFile()
        Gson gson = new GsonBuilder().setPrettyPrinting().disableHtmlEscaping().create()
        TreeMap<String,String> EN_US = gson.fromJson(createInput(mainLangJson), TreeMap<String,String>.class)
        if(EN_US == null) {
            println "Skipping langFile because of incorrect structure " + mainLangJson.getName()
            return
        }

        File[] otherLangs = langFolder.listFiles( (FileFilter) (f) -> {return f != mainLangJson && f.isFile() && f.getName().endsWith(".json")})
        println "\u001B[32m- Comparing langs with:\u001B[0m " + mainLangJson.getName()
        for (final File otherLang in otherLangs) {
            InputStreamReader reader = createInput(otherLang)
            TreeMap<String,String> LANG = gson.fromJson(reader, TreeMap<String,String>.class)
            reader.close();
            if(LANG == null) {
                println "Skipping langFile because of incorrect structure " + otherLang.getName()
                continue
            }

            EN_US.forEach(LANG::putIfAbsent)
            OutputStreamWriter writer = createOutput(otherLang)
            gson.toJson(LANG, TreeMap<String, String>.class, writer)
            writer.close()
            println "\u001B[32m- Compared lang file\u001B[0m " + "\u001B[36m" + otherLang.getName() + "\u001B[0m"
        }
    }
}

processResources {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

jar.finalizedBy('reobfJar')
publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact jar
        }
    }
    repositories {
        maven {
            url = "file:///${project.projectDir}/../mcmodsrepo"
        }
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8' // Use the UTF-8 charset for Java compilation
}

// Produces the source-jar, which is for JitPack
task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = "sources"
    from sourceSets.main.allSource
}

modrinth {
    token = System.getenv("modrinthtoken") // Remember to have the MODRINTH_TOKEN environment variable set or else this will fail - just make sure it stays private!
    projectId = "valoriadev"
    versionNumber = "${dev_version}"
    versionType = "alpha"
    versionName = "Dev Build - ${dev_version}"
    uploadFile = tasks.jar
    changelog = "Just a DevBuild to test things, its some sorta like minecraft snapshots to find bugs and report them"
    dependencies {
        required.project "curios"
    }
}

artifacts {
    archives jar
}